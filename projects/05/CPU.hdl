// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:

    // i  _  _  a  c1 c2 c3 c4 c5 c6 d1 d2 d3 j1 j2 j3
    // 15 14 13 12 11 10 09 08 07 06 05 04 03 02 01 00

    Not(in=instruction[15], out=notA);

    Mux16(a=instruction, b=preAluOutput, sel=instruction[15], out=aRegisterInput);

    And(a=instruction[15], b=instruction[5], out=cInstructionSaveA);

    Or(a=notA, b=cInstructionSaveA, out=shouldSaveToARigster);

    ARegister(in=aRegisterInput, load=shouldSaveToARigster, out=aRegisterOutput);

    And16(a=aRegisterOutput, b=aRegisterOutput, out[0..14]=addressM);
    
    And(a=instruction[15], b=instruction[12], out=AMSwitch);
    Mux16(a=aRegisterOutput, b=inM, sel=AMSwitch, out=aluYBit);
    
    ALU(
        x=preDRegister, 
        y=aluYBit, 
        zx=instruction[11],
        nx=instruction[10],
        zy=instruction[9],
        ny=instruction[8],
        f=instruction[7],
        no=instruction[6],
        out=aluOutput,
        zr=isAluOutputZero,
        ng=isAluOutputNagtive
    );

    And(a=instruction[15], b=instruction[4], out=shouldSaveDRegister);
    DRegister(in=aluOutput, load=shouldSaveDRegister, out=preDRegister);
    And16(a=aluOutput, b=aluOutput, out=preAluOutput);

    And16(a=aluOutput, b=aluOutput, out=outM);

    And(a=instruction[15], b=instruction[3], out=writeM);
    
    And(a=isAluOutputNagtive, b=instruction[2], out=shouldJumpWhenNagtive);
    And(a=isAluOutputZero, b=instruction[1], out=shouldJumpWhenZero);

    Not(in=isAluOutputZero, out=aluOutputNotZero);
    Not(in=isAluOutputNagtive, out=aluOutputNotNagtive);
    And(a=aluOutputNotZero, b=aluOutputNotNagtive, out=isAluOutputPositive);
    And(a=isAluOutputPositive, b=instruction[0], out=shouldJumpWhenPositive);

    Or(a=shouldJumpWhenZero, b=shouldJumpWhenNagtive, out=shouldJumpWhenLessOrEqual);
    Or(a=shouldJumpWhenLessOrEqual, b=shouldJumpWhenPositive, out=metAtLeastOneJumpBit);

    // special case when jump bits are 111
    And(a=instruction[2], b=instruction[1], out=compareD2AndD1);
    And(a=compareD2AndD1, b=instruction[0], out=areTheyAllOnes);

    Or(a=areTheyAllOnes, b=metAtLeastOneJumpBit, out=jumpBitResult);

    // If A instruction don't jump
    And(a=jumpBitResult, b=instruction[15], out=shouldJump);

    PC(in=aRegisterOutput, load=shouldJump, inc=true, reset=reset, out[0..14]=pc);
}
